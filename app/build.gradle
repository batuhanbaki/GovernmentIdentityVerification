plugins {
    id 'com.android.library'
    id 'maven-publish'
}

def githubProperties = new Properties()
githubProperties.load(new FileInputStream(rootProject.file("github.properties")))
def getArtificatId = { ->
    return "identitycheck"
}
def buildTime = new Date().format("yyMMddHHmm", TimeZone.getTimeZone("Asia/Istanbul"))
def getVersionName = { ->
    return "1.0.0"
}

android {
    compileSdk 32

    defaultConfig {
        minSdk 21
        targetSdk 32
        versionCode 1
        versionName "${getVersionName()}"
        buildConfigField 'String', 'buildTime', "\"${buildTime}\""
        buildConfigField 'String', 'SOAP_ACTION', "\"http://tckimlik.nvi.gov.tr/WS/TCKimlikNoDogrula\""
        buildConfigField 'String', 'METHOD_NAME', "\"TCKimlikNoDogrula\""
        buildConfigField 'String', 'NAME_SPACE', "\"http://tckimlik.nvi.gov.tr/WS\""
        buildConfigField 'String', 'URL', "\"https://tckimlik.nvi.gov.tr/Service/KPSPublic.asmx?WSDL\""
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.all {
            outputFileName = "${getArtificatId()}.aar"

        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'com.google.android.material:material:1.6.1'
    testImplementation 'junit:junit:4.13.2'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.4.0'
    implementation files('libs/soap.jar')
}


publishing {
    publications {
        bar(MavenPublication) {
            groupId 'com.batuhanbaki'
            artifactId getArtificatId()
            version getVersionName()
            artifact("$buildDir/outputs/aar/${getArtificatId()}.aar")
        }
    }

    repositories {
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/batuhanbaki/identitycheck")
            credentials {
                username = githubProperties['gpr.usr'] ?: System.getenv("GPR_USER")
                password = githubProperties['gpr.key'] ?: System.getenv("GPR_API_KEY")
            }
        }
    }
}